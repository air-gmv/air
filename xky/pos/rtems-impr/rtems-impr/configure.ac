## Process this file with autoconf to produce a configure script.
## 
## $Id: configure.ac 5659 2009-11-24 17:23:00Z jsousa $

AC_PREREQ(2.60)
AC_INIT([rtems],[_RTEMS_VERSION],[http://www.rtems.org/bugzilla])
AC_CONFIG_SRCDIR([c])
RTEMS_TOP([.])

# Abort if trying to build inside of the source tree.
AS_IF([test -f aclocal/version.m4],[
  rm -f config.cache config.log confdefs.h
  AC_MSG_ERROR([***]
    [Attempt to build inside of the source tree]
    [Please use a separate build directory, instead] )
])

AC_CANONICAL_TARGET([])
AM_INIT_AUTOMAKE([no-define foreign dist-bzip2 no-dist-gzip 1.10])
AM_MAINTAINER_MODE

## These option are only in here to let --help report all supported
## options.
RTEMS_ENABLE_MULTIPROCESSING
RTEMS_ENABLE_NETWORKING
RTEMS_ENABLE_CXX
RTEMS_ENABLE_RTEMS_DEBUG
RTEMS_ENABLE_RTEMSBSP
RTEMS_ENABLE_MULTILIB
RTEMS_ENABLE_COVERAGE
RTEMS_ENABLE_PERFORMANCE_DIRECTIVES
RTEMS_ENABLE_PERFORMANCE_CRITICAL

AC_ARG_ENABLE([docs],
  [AC_HELP_STRING([--enable-docs],[enable building documentation
    (default:disabled)])])

AS_IF([test x"${enable_docs}" = x"yes"],
  [RTEMS_BUILD_CONFIG_SUBDIRS([doc])])

AS_IF([test x"$enable_multilib" = x"yes"],[
  RTEMS_TARGET_CONFIG_SUBDIRS([cpukit])
])
RTEMS_TARGET_CONFIG_SUBDIRS([c])

AC_CONFIG_FILES([Makefile
make/Makefile
])

AC_OUTPUT
